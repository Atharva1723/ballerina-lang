/*
 * Copyright (c) 2025, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'base'
    id 'javaProject'
}

description = 'Ballerina - Bal tools Test'

def extractedDist = "$buildDir/extractedDistribution/jballerina-tools-${project.version}"

configurations {
    jballerinaTools
}

dependencies {
    jballerinaTools project(path: ':jballerina-tools', configuration: 'zip')
    testImplementation libs.testng
    testImplementation libs.slf4j.jdk14
    testImplementation libs.gson
    implementation libs.picocli

    implementation project(path: ':ballerina-test-utils')
    implementation project(':ballerina-runtime')
}

tasks.register('extractDistribution', Copy) {
    dependsOn ':jballerina-tools:build'
    from zipTree(configurations.jballerinaTools.asPath)
    into "$buildDir/extractedDistribution"

}

tasks.register('copyTestResources') {
    doLast {
        copy {
            from 'src/test/resources/dist-tools/bal-tools.toml'
            into "$buildDir/extractedDistribution/jballerina-tools-${project.version}/resources/"
        }
        copy {
            from 'src/test/resources/central-tools/bal-tools.toml'
            into "$buildDir/user-home/.ballerina/.config/"
        }
        copy {
            from 'src/test/resources/central-tools'
            into "$buildDir/user-home/.ballerina/repositories/central.ballerina.io/bala/"
            include '**'
        }
        copy {
            from 'src/test/resources/dist-tools'
            into "$buildDir/extractedDistribution/jballerina-tools-${project.version}/repo/bala/ballerina/"
            include '**'
        }
        copy {
            from 'src/test/resources/projects'
            into "$buildDir/projects/"
            include '**'
        }
    }
}

// Integration tests for bal tools
test {
    dependsOn extractDistribution
    dependsOn copyTestResources

    maxParallelForks = 1
    systemProperty 'server.path', extractedDist
    environment 'BALLERINA_HOME_DIR', "$buildDir/user-home/.ballerina"
    environment 'PROJECT_VERSION', project.version

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}
